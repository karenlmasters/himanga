PRO asciiout,reducer
; A program to create a ascii data file for a reduced HI profile, in
; order to make plotting easier. 
; Version of March 2017
; Based on parts of write_ascii.pro, header.pro
; 
	if (n_elements(reducer) eq 0) then $
 	reducer='HI-MaNGA Team'

; Code from header.pro
        if (n_elements(dc) eq 0) then dc = !g.s[0]
        if (size(dc,/type) eq 2) then dc = !g.s[dc]
        isvalid = data_valid(dc,name=name)
        if (name ne 'SPECTRUM_STRUCT') then begin
           message,"header does not yet work in continuum mode.",/info
           return
        end
        if (isvalid eq -1) then begin
           message,'dc must be either continuum or spectrum', /info
           return
        endif
        if (isvalid eq 0) then begin
           message,'No spectrum is available.',/info
           return
        endif

        root=strtrim(dc.source)
	file=root+'.txt'
	openw,lun,file,/get_lun	

        radecValue = getradec(dc,/quiet)
        radec=strtrim(adstring(radecValue[0],radecValue[1],0),2) ; RA,DEC string
        date = leftjustify(dc.date,11)  ;Date of observations
        Tint = leftjustify(string(dc.exposure,format='(F8.1)'),8,1)
        vel=leftjustify(string(dc.source_velocity/1.0e3,format='(F9.1)'),9,1)

        printf,lun,'################################################################################'
        printf,lun,'# HI-MaNGA: HI Followup for the MaNGA Survey' 
        printf,lun,'# Observed under code: GBT16A_095'
        printf,lun,'# Masters, K.L. et al. in prep.'
        printf,lun,'################################################################################'
        printf,lun,'# Telescope:   Robert C. Bryd Green Bank Telescope'
        printf,lun,'# Beam Size:   9.0 [arcminutes] FWHM'
        printf,lun,'# Object (MaNGA plate-ifu format): ', root
        printf,lun,'# RA Dec (J2000): ', radec
        printf,lun,'# Rest Frequency  1420.4058 [MHz]'
        printf,lun,'# Central velocity [km/s]: ',vel 
        printf,lun,'# Integration time [seconds] is: ', Tint
        printf,lun,'# Date(s) of observations (UT): ', date
        printf,lun,'################################################################################'
        printf,lun,'# ASCII Table'
        printf,lun,'# Generated by ',reducer, '  ', systime()
        printf,lun,'# The spectrum is baselined and Hanning smoothed'
        printf,lun,'# Columns are: Velocity of HI (km/s), Flux (mJy)'
        printf,lun,'################################################################################'

        if (getxunits() ne 'km/s') then begin
           print,'Change x-units to km/s'
           stop
        endif

	x=getxarray(count=nch)
	y=getyarray(count=nch)
	for i = 0, nch-1, 1 do $
	if (FINITE(y[i]) eq 1) then printf,lun,x[i],y[i]
      
	close,lun
	free_lun,lun			

END

